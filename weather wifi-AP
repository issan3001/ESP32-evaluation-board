#include <Arduino.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <Adafruit_AHTX0.h>
#include <Adafruit_BMP280.h>
#include <WiFi.h>
#include <WebServer.h>
#include <Preferences.h>

#define LED_R     15
#define LED_G     2
#define LED_B     4
#define SWITCH_1  12
#define SWITCH_2  14
#define SWITCH_3  26
#define SWITCH_4  27

int colorMode = 0; // 0=OFF 1=RED 2=GREEN 3=BLUE


// ---------- Wifi Setting ----------
const char* ap_ssid = "ESP32_WeatherAP";
const char* ap_password = "12345678";

// ---------- OLED ----------
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// ---------- bmp ----------
Adafruit_AHTX0 aht;
Adafruit_BMP280 bmp;

// ---------- WiFi ----------
Preferences prefs;
int apChannel = 1;
int nextChannel = 1;
bool rebootNeeded = false;

// ----------timer ----------
unsigned long lastCheck = 0;
const unsigned long CHECK_INTERVAL = 300000; // 5分

// ---------- Web ----------
WebServer server(80);

// ---------- wifi channel scan（only start up） ----------
int scanBestChannelOnce() {
  WiFi.mode(WIFI_STA);
  delay(100);

  int count[14] = {0};
  int n = WiFi.scanNetworks();
  if (n <= 0) return apChannel;

  for (int i = 0; i < n; i++) {
    int ch = WiFi.channel(i);
    if (ch >= 1 && ch <= 13) count[ch]++;
  }

  int best = 1;
  int minVal = count[1];
  for (int ch = 2; ch <= 13; ch++) {
    if (count[ch] < minVal) {
      minVal = count[ch];
      best = ch;
    }
  }
  return best;
}

// ================= SETUP =================
void setup() {
  pinMode(LED_R, OUTPUT);
  pinMode(LED_G, OUTPUT);
  pinMode(LED_B, OUTPUT);

  pinMode(SWITCH_1, INPUT_PULLUP);
  pinMode(SWITCH_2, INPUT_PULLUP);
  pinMode(SWITCH_3, INPUT_PULLUP);
  pinMode(SWITCH_4, INPUT_PULLUP);

  digitalWrite(LED_R, LOW);
  digitalWrite(LED_G, LOW);
  digitalWrite(LED_B, LOW);

  Wire.begin(21, 22);

  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) while (1);
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("BOOTING...");
  display.display();

  if (!aht.begin() || !bmp.begin(0x77)) while (1);

  prefs.begin("wifi", false);
  apChannel = prefs.getInt("ap_ch", 1);

  int best = scanBestChannelOnce();
  if (best != apChannel) {
    apChannel = best;
    prefs.putInt("ap_ch", best);
  }

  WiFi.mode(WIFI_AP);
  WiFi.softAP(ap_ssid, ap_password, apChannel);

  // Web
  server.on("/", []() {
    server.send(200, "text/plain", "ESP32 Weather Station");
  });
  server.begin();
}

// ================= LOOP =================
void loop() {
  // ---------- button in ----------
if (digitalRead(SWITCH_1) == LOW) colorMode = 1;
if (digitalRead(SWITCH_2) == LOW) colorMode = 2;
if (digitalRead(SWITCH_3) == LOW) colorMode = 3;
if (digitalRead(SWITCH_4) == LOW) colorMode = 0;

// ---------- LED out ----------
switch (colorMode) {
  case 1: // RED
    digitalWrite(LED_R, HIGH);
    digitalWrite(LED_G, LOW);
    digitalWrite(LED_B, LOW);
    break;
  case 2: // GREEN
    digitalWrite(LED_R, LOW);
    digitalWrite(LED_G, HIGH);
    digitalWrite(LED_B, LOW);
    break;
  case 3: // BLUE
    digitalWrite(LED_R, LOW);
    digitalWrite(LED_G, LOW);
    digitalWrite(LED_B, HIGH);
    break;
  default: // OFF
    digitalWrite(LED_R, LOW);
    digitalWrite(LED_G, LOW);
    digitalWrite(LED_B, LOW);
    break;
}

  unsigned long now = millis();
  if (now - lastCheck > CHECK_INTERVAL) {
    lastCheck = now;
    rebootNeeded = true;
  }

  sensors_event_t h, t;
  aht.getEvent(&h, &t);
  float p = bmp.readPressure() / 100.0;

  // OLED
  display.clearDisplay();
  display.setCursor(0, 0);
  display.printf("SSID:%s\n", ap_ssid);
  display.printf("IP:%s\n", WiFi.softAPIP().toString().c_str());
  display.printf("CH:%d\n", apChannel);

  if (rebootNeeded) {
    display.println("OPTIMIZE ON");
    display.println("NEXT BOOT");
  } else {
    display.println("CH OK");
  }

  display.printf("T:%.1fC H:%.1f%%\n", t.temperature, h.relative_humidity);
  display.printf("P:%.1fhPa\n", p);
  display.display();

  server.handleClient();
  delay(500);
}
